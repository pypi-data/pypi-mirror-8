1 CLS
2 PRINT "KEYBOARD INFO V0.1 (GPL V3+)"
3 PRINT "COPYLEFT (C) 2014 JENS DIEMER"
5 PRINT @ 64, "*** PRESS ANY KEY! ***"
6 PRINT @ 384, "------ LIVE FROM MEMORY: ------"
7 PRINT @ 416, "152 153 154 155 156 157 158 159"
8 GOSUB 1000
10 C0=PEEK(&H0152) ' COLUMN 1'S BYTE
20 C1=PEEK(&H0153) ' COLUMN 2'S BYTE
30 C2=PEEK(&H0154) ' COLUMN 3'S BYTE
40 C3=PEEK(&H0155) ' COLUMN 4'S BYTE
50 C4=PEEK(&H0156) ' COLUMN 5'S BYTE
60 C5=PEEK(&H0157) ' COLUMN 6'S BYTE
70 C6=PEEK(&H0158) ' COLUMN 7'S BYTE
80 C7=PEEK(&H0159) ' COLUMN 8'S BYTE
90 I$=INKEY$
110 IF I$="" THEN 150
120 L$=I$
130 H$=HEX$(ASC(I$))
140 PRINT @ 64, "** LAST KEY: '"+ I$ +"' ASCII: $"+H$+" **"
150 PRINT @ 448, " "+HEX$(C0)+"  "+HEX$(C1)+"  "+HEX$(C2)+"  "+HEX$(C3)+"  "+HEX$(C4)+"  "+HEX$(C5)+"  "+HEX$(C6)+"  "+HEX$(C7)
160 GOTO 10
1000 PRINT @ 96, "------- DRAGON KEY MAP: -------"
1010 PRINT "PB0 PB1 PB2 PB3 PB4 PB5 PB6 PB7"
1020 PRINT " 0   1   2   3   4   5   6   7 "
1030 PRINT " 8   9   *   ;   ,   -   .   / "
1040 PRINT " @   A   B   C   D   E   F   G "
1050 PRINT " H   I   J   K   L   M   N   O "
1060 PRINT " P   Q   R   S   T   U   V   W "
1070 PRINT " X   Y   Z  UP  DWN LEF RIG SPC"
1080 PRINT "ENT CLR BRK               SHIFT"
1090 RETURN

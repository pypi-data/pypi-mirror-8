10 'DUMP PROGRAM
15 'Peeks a word (16 bits)
20 DEF FNW(A)=PEEK(A)*256+PEEK(A+1)
30 DIM ZZ(2),XX(2,1),ZZ$(2),XX$(2,1)
40 DIM XY(1,2,3)
50 'DEFINING VARIABLES BEFORE USE
60 FOR I=0 TO 2
70 ZZ(I)=I:ZZ$(I)=STR$(I)
80 FOR J=0 TO 1
90 XX(I,J)=C:XX$(I,J)=STR$(C)
100 XY(J,I,0)=C
110 C=C+1
120 NEXTJ,I
130 PS=0 'Program Start
140 PE=0 'Program End
150 VS=0 'Variables Start
160 VE=0 'Variables End
170 AS=0 'Array Start
180 AE=0 'Array End
190 DA=0 'Dump Address
200 NA=0 'Next Address
210 DV=0 'Device number
220 DBYTE=0:AN$="":SBYTE=0
230 R=0:H3=0:H2=0:H1=0:H0=0
240 PA=&H19
250 PS=FNW(PA) '19:1A contain PS address
260 VS=FNW(PA+2) '1B:1C contain VS address
270 AS=FNW(PA+4) '1D:1E contain AS address
280 PE=VS-1 'Variables are after program
290 VE=AS-1 'Arrays are after variables
300 AE=FNW(PA+6)-1 '1F:20 holds free space address
310 CLS
320 INPUT"OUTPUT TO PRINTER";AN$
330 IF AN$ = "Y" THEN DV=-2 ELSE DV=0
340 PRINT#DV,"PROGRAM START ADDRESS = ";:DBYTE=PS
350 GOSUB 1000:PRINT#DV
360 PRINT#DV,"PROGRAM END ADDRESS = ";:DBYTE=PE
370 GOSUB 1000:PRINT#DV
380 PRINT#DV,"VARIABLE START ADDRESS = ";:DBYTE=VS
390 GOSUB 1000:PRINT#DV
400 PRINT#DV,"VARIABLE END ADDRESS = ";:DBYTE=VE
410 GOSUB 1000:PRINT#DV
420 PRINT#DV,"ARRAY START ADDRESS = ";:DBYTE=AS
430 GOSUB 1000:PRINT#DV
440 PRINT#DV,"ARRAY END ADDRESS = ";:DBYTE=AE
450 GOSUB 1000:PRINT#DV
460 INPUT "DO YOU WISH A PROGRAM DUMP";AN$
470 IF AN$<>"Y" THEN GOTO 630
480 PRINT#DV,"PROGRAM DUMP"
490 DA=PS
500 NA=FNW(DA)
510 IF (NA=0) OR (DA=PE) THEN GOTO 620
520 PRINT#DV:PRINT#DV
530 DBYTE=DA:GOSUB 1000 'PRINT CURRENT ADDRESS
540 PRINT#DV
550 DBYTE=NA: GOSUB 1000 'PRINT NEXT ADDRESS
560 DA=DA+2
570 DBYTE=FNW(DA) : GOSUB 1000 'PRINT LINE NUMBER
580 DA=DA+2
590 SBYTE=PEEK(DA): GOSUB 2000 'PRINT LINE CONTENTS
600 DA=DA+1
610 IF DA<>NA THEN GOTO 590 ELSE GOTO 500
620 PRINT#DV
630 INPUT "DO YOU WANT A VARIABLE DUMP";AN$
640 IF AN$ <> "Y" THEN GOTO 690
650 PRINT#DV:PRINT#DV:PRINT#DV,"VARIABLE DUMP"
660 FOR DA = VS TO VE
670 SBYTE=PEEK(DA): GOSUB 2000
680 NEXT DA
690 PRINT#DV
700 INPUT "DO YOU WANT AN ARRAY DUMP";AN$
710 IF AN$ <> "Y" THEN GOTO 760
720 PRINT#DV:PRINT#DV:PRINT#DV,"ARRAY DUMP"
730 FOR DA = AS TO AE
740 SBYTE=PEEK(DA): GOSUB 2000
750 NEXT DA
760 PRINT#DV:PRINT#DV,"DUMP FINISHED"
770 STOP
1000 'PRINT 2 BYTES AS 4 HEX CHARS
1010 R=DBYTE
1020 H3=INT(R/4096): R=DBYTE-H3*4096
1030 H2=INT(R/256): R=R-H2*256
1040 H1=INT(R/16): H0=R-H1*16
1045 HW$=HEX$(H3)+HEX$(H2)+HEX$(H1)+HEX$(H0)
1050 PRINT#DV, USING "%  %"; HW$
1060 RETURN
2000 'PRINT 1 BYTE AS 2 HEX CHARS
2010 H1=INT(SBYTE/16): H0=SBYTE-H1*16
2020 PRINT#DV, USING"% %";HEX$(H1)+HEX$(H0);
2030 RETURN
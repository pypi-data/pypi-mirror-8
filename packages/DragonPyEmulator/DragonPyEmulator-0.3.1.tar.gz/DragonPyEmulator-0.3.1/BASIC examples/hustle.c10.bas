10 CLEAR1000:X=RND(-(PEEK(9)*256+PEEK(10))):GOTO40
20 CLS:A$="":IZ=0:IP=1:IN=-1:X4=0:X2=0:X=39:Y=15:B=256:A=255:X1=1:Y1=0:F3=0:F=0:F0=-3:F4=0:IS=0:X2=0:Q=0:I0=0
30 IB=-1:P=0:P1=.9:IT=1200:IQ=0:F7=16:F8=39:FZ=32:MC=16384:I8=0:FV=128:F9=32:RETURN
40 GOSUB20:DIMI(IT):PRINT@197,"C O L O R  H U S T L E"
50 PRINT@354,"DO YOU WANT DIRECTIONS <Y/N>?";
60 A$=INKEY$:IFA$="N"THEN360
70 IFA$="Y"THEN100
80 GOTO60
90 CLS:PRINT:PRINTTAB(5)"C O L O R  H U S T L E":PRINT:RETURN
100 GOSUB90
110 PRINT"THE OBJECT IS TO SCORE AS MANY"
120 PRINT"POINTS AS POSSIBLE BY REACHING"
130 PRINT"THE TARGET WITHOUT TOUCHING A"
140 PRINT"LINE OR REVERSING UPON"
150 PRINT"YOURSELF. YOUR RATING REFERS TO"
160 PRINT"THE NUMBER OF TARGETS THAT YOU"
170 PRINT"GOT MULTIPLIED BY YOUR SCORE,"
180 PRINT"AND DIVIDED BY THE TIME."
190 PRINT:PRINT"<N>EXT SCREEN"
200 A$=INKEY$
210 IFA$="N"THEN230
220 GOTO200
230 GOSUB90
240 PRINT:PRINT"COMMANDS ARE:"
250 PRINTCHR$(87),"MOVE UP"
260 PRINTCHR$(90),"MOVE DOWN"
270 PRINTCHR$(65),"MOVE LEFT"
280 PRINTCHR$(83),"MOVE RIGHT"
290 PRINT"T","TO STOP PLAYING"
300 PRINT:PRINT"TO START PRESS THE <ENTER> KEY"
310 PRINT"<P>REVIOUS SCREEN"
320 A$=INKEY$
330 IFA$=CHR$(13)THEN360
340 IFA$="P"THEN100
350 GOTO320
360 GOSUB820:REM DRAW SCREEN
370 GOSUB400:GOSUB480:GOSUB730:GOSUB850:GOTO370
380 FORD=1TO4:NEXTD:RETURN
390 REM NEXT SBR. PROCESSES COMMANDS
400 A$=INKEY$:IFA$=""THENRETURN
410 IFA$="T"THENRUN
420 Q=ASC(A$):IFQ=65THENX1=IN:Y1=IZ:RETURN
430 IFQ=83THENX1=IP:Y1=IZ:RETURN
440 IFQ=87THENX1=IZ:Y1=IN:RETURN
450 IFQ=90THENX1=IZ:Y1=IP:RETURN
460 RETURN
470 REM NEXT SBR. RESETS POINTER AND MOVES DOT
480 X=X+X1:Y=Y+Y1:F3=F3+IP:IFF3>ITTHENF3=IZ:IQ=IQ+IT
490 IFF0<IZTHENF0=F0+IP:GOTO510
500 F=F+IP:IFF>ITTHENF=IZ
510 U=I(F):RESET(UANDA,U/B):I(F3)=Y*B+X:IFPOINT(X,Y)THEN530
520 SET(X,Y,6):RETURN
530 P=INT((Y/2))*32+INT(X/2):ON1-(P=F4ORP=F4+1ORP=F4+2)GOTO600
540 Q=RND(9):IS=IS+Q:F0=F0-Q*2:I8=I8+1:FORI=IPTO8:PRINT@F4,STR$(Q)" ";:GOSUB380
550 PRINT@F4,CHR$(143+16)+CHR$(143+16)+CHR$(143+16);:PRINT@F4,CHR$(143+48)+CHR$(143+48)+CHR$(143+48);:NEXT
560 X2=INT(I8*IS*1000/(IQ+F3)):IFX2>X4THENX4=X2
570 PRINT@F4,"XXX";:F4=IP:IB=IN:PRINT@456,IQ+F3;:PRINT@472,IS;:PRINT@488,X;:PRINT@504,X4;
580 GOTO520
590 REM YOU LOSE HERE
600 GOSUB920:CLS:PRINT@107,"GAME OVER":PRINT@161,"FINAL SCORE:";IS:PRINT@193,"CURRENT RATING:";X2
610 PRINT@225,"HIGHEST RATING IN GAME:";X4
620 FORI=IZTOTI:I(I)=IZ:NEXT I
630 IFIS>HITHENHI=IS
640 IFX4>X5THENX5=X4
650 PRINT@9*32," HIGHEST SCORE SO FAR:";HI
660 PRINT@10*32," HIGHEST RATING SO FAR:";X5
670 PRINT@480,"   HIT <ENTER> FOR NEW GAME";
680 A$=INKEY$
690 IFA$=CHR$(13)THEN710
700 GOTO680
710 GOSUB20:GOSUB820:RETURN
720 REM THIS SUBR PUTS BOXES ON SCREEN
730 IFIB>IZTHENIB=IB-IP:RETURN
740 IFIB=IZTHENPRINT@F4,"aaa";:IB=IN
750 IFRND(IZ)<P1THENRETURN
760 C=0
770 C=C+IP:IFC>10THENRETURN
780 T1=RND(12):T2=RND(28):F4=T1*32+T2
790 FORI=MC+F4TOMC+F4+2:U=PEEK(I):IFU<>208THENI=MC+511:NEXT:GOTO770
800 NEXT:PRINT@F4,"yyy";:IB=RND(65)+25:RETURN
810 REM FOLOWING SUBR. DRAWS SCREEN
820 CLS:FORI=0TO13:PRINT@I*32,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX";:NEXT:FORI=1TO26:SET(1,I,3):SET(62,I,3):NEXT
830 FORI=1TO62:SET(I,1,3):SET(I,26,3):NEXT
840 PRINT@14*32,"TIME";:PRINT@14*32+16,"SCORE";:PRINT@15*32,"RATING";:PRINT@15*32+16,"HIGHEST";:RETURN
850 FORI=1TO15:NEXTI:RETURN
860 REM -- COLOR HUSTLE --
870 REM BY PETER TREFONAS,    U.N.O. (12/6/78)
880 REM MC-10 COLOR ADAPTATION BY ERIC UNDERHILL (7/28/2013)
890 REM FROM A MODIFIED SOURCE BY P. J. HYDE (10/10/79)
900 REM SOME ADDITIONAL EDITS BY JIM GERRIE (8/12/2013)
910 REM EXPLOSION ANIMATION BY ROBERT SIEG (1/8/2014)
920 READA:ONA+5GOTO960,950,920,940:PS=A+SC:READA:X=MC+P+PS-109:ON1-(X>MCANDX<MC+448)GOTO920,930
930 POKEX,A:GOTO920
940 SC=0:GOTO920
950 GOSUB970:GOTO920
960 RESTORE:RETURN
970 FORZ=1TO100:NEXTZ:RETURN
980 DATA-1,109,177,142,184,110,162,141,164,109,184,142,177,76,177,110,166,141,166,175,184
990 DATA76,168,79,162,110,178,141,180,172,164,175,161,79,180,172,162,79,164,107,164,110,162
1000 DATA144,168,111,180,140,178,79,128,107,128,110,128,172,128,76,128,142,128,111,128,141,128
1010 DATA109,128,175,128,140,128,144,128,-3,-3,-3,-3,-3,-3,-3,-4
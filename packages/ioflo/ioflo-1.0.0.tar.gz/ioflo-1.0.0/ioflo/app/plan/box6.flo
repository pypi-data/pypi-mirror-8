# example mission box6.flo
# uses conditional auxiliary

house box6

init limit.depth.max to 50.0
init limit.depth.shallow to 10.0
init limit.depth.cruise to 5.0

init boxer.heading to 0.0
init boxer.turn to 90.0
init boxer.leg to 0.0
init boxer.nlegs to 4.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first leg

frame depthmax
   go abort if depth >= .limit.depth.max

frame shallow in depthmax
   aux shallow if depth >= .limit.depth.shallow # until aux done

frame leg in shallow
   set elapsed to 60.0
   set heading by boxer.heading
   set depth to 15.0
   set speed to 2.5
   go gpsfix if elapsed >= goal 
   go mission_stop if .boxer.leg >= .boxer.nlegs 

frame gpsfix
   aux gps
   go leg if done gps
   exit
      inc boxer.heading by boxer.turn
      inc boxer.leg with 1.0

frame abort
   set depth with 0.0
   set speed with 2.5
   go mission_stop if depth == 0.0 +- 0.25 

frame mission_stop  
   bid stop all

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed
   do controller pid heading
   do controller pid depth
   do controller pid pitch

framer shallow be aux first shallow1 # conditional auxiliary

frame shallow1
   set depth to 0.0
   set speed to 2.5
   go next if depth <= .limit.depth.cruise #repaired

frame shallow2 #so complete
   let me if depth <= .limit.depth.cruise # dumb beact
   done
   
logger logger to /var/tmp/log/ioflo/
log state   on update
loggee position state.position heading state.heading depth state.depth speed state.speed

log goal   on update
loggee heading goal.heading depth goal.depth speed goal.speed duration goal.duration

load gps.flo

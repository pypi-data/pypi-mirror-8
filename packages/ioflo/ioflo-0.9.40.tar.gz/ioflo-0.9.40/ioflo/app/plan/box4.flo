#example mission box4.flo
# with refactored leg iteration using counter

house box4

init max.depth to 50.0

init boxer.heading to 0.0
init boxer.turn to 90.0
init boxer.leg to 0.0
init boxer.nlegs to 4.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first leg

frame depthmax
   go abort if depth >= .max.depth

frame leg in depthmax
   set elapsed to 20.0
   set heading from boxer.heading
   set depth to 5.0
   set speed to 2.5
   go next if elapsed >= goal
   go mission_stop if .boxer.leg >= .boxer.nlegs

frame gpsfix
   aux gps
   go next if done gps

frame turn in depthmax
   inc boxer.heading from boxer.turn
   inc boxer.leg with 1.0
   go leg

frame abort
   set depth to 0.0
   set speed to 2.5
   go mission_stop if depth == 0.0 +- 0.25 

frame mission_stop  
   bid stop all

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed with fake 1
   do controller pid heading 
   do controller pid depth from value in .max.depth
   do controller pid pitch
   
logger logger to /var/tmp/log/ioflo/
log state  on update
loggee position state.position heading state.heading depth state.depth speed state.speed

log goal on update
loggee heading goal.heading depth goal.depth speed goal.speed

framer gps be aux first gps1

frame gps1
   set depth to 0.0
   go next if depth == 0.0 +- 0.25
   
frame gps2
   set speed to 0.0
   go next if elapsed >= 10.0

frame gps3
   done

